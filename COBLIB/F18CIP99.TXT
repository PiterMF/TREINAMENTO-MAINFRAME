      *==================================================               00001001
       IDENTIFICATION DIVISION.                                         00002001
      *==================================================               00003001
                                                                        00004001
       PROGRAM-ID. F18CIP99.                                            00005001
                                                                        00006001
      *==========================================================*      00007001
      *  INSTRUTOR  : IVAN SANCHES                               *      00008001
      *  AUTOR      : PITER FANTI                                *      00009001
      *  EMPRESA    : FOURSYS                                    *      00010001
      *----------------------------------------------------------*      00020001
      *  VERSAO   DATA       AUTOR     DESCRICAO DA MODIFICACAO  *      00020101
      *   1.0   28/06/22  PITER FANTI  VERSAO INICIAL            *      00020201
      *----------------------------------------------------------*      00020301
      *  OBJETIVO   : PROGRAMA DE LEITURA DO MAPA F18CIM1 PELO   *      00020401
      *               VSAM (REGISTRO DO PROGRAMA F18CIP2 COM     *      00020501
      *               DIFERENCAS LOGICAS DE FUNCIONALIDADE)      *      00020601
      *==========================================================*      00020701
                                                                        00020801
      *==================================================               00020901
       DATA DIVISION.                                                   00021001
      *==================================================               00021101
       WORKING-STORAGE SECTION.                                         00022001
      *==================================================               00023001
                                                                        00024001
           COPY F18CIM1.                                                00025001
            COPY DFHAID.                                                00026001
                                                                        00027001
      *-------------------------------------------                      00028001
       01 FILLER PIC X(70)                                              00029001
              VALUE 'VARIAVEL DE RECEBIMENTO DE DADOS'.                 00030001
      *-------------------------------------------                      00040001
       01 WRK-DADOS.                                                    00041001
          05 WRK-CHAVE    PIC X(05).                                    00042001
          05 WRK-NOME     PIC X(30).                                    00043001
          05 WRK-SETOR    PIC X(04).                                    00044001
          05 WRK-SALARIO  PIC X(10).                                    00045001
          05 WRK-DATADIA  PIC X(02).                                    00046001
          05 FILLER       PIC X(01) VALUE '/'.                          00047001
          05 WRK-DATAMES  PIC X(02).                                    00047101
          05 FILLER       PIC X(01) VALUE '/'.                          00047201
          05 WRK-DATAANO  PIC X(04).                                    00047301
          05 WRK-EMAIL    PIC X(40).                                    00047401
                                                                        00047501
      *----------------------------------------------                   00047601
       01 FILLER PIC X(70)                                              00047701
              VALUE 'VARIAVEIS DE STATUS E FLAG'.                       00047801
      *----------------------------------------------                   00047901
       77 WRK-RESP        PIC S9(04) COMP.                              00048001
       77 WRK-RET-MAPA    PIC S9(04) COMP.                              00049001
       77 WRK-FLAG        PIC  9(01).                                   00050001
       77 WRK-SAIDA       PIC  X(30) VALUE '-----FIM-----'.             00051001
                                                                        00052001
      *==================================================               00053001
       PROCEDURE                         DIVISION.                      00054001
      *==================================================               00055001
                                                                        00056001
      *----------------------------------------------------             00057001
       0000-PRINCIPAL                            SECTION.               00058001
      *----------------------------------------------------             00059001
           PERFORM 1000-INICIAR.                                        00060001
           IF WRK-FLAG EQUAL 1                                          00061001
              PERFORM 2000-PROCESSAR                                    00062001
           END-IF.                                                      00063001
           PERFORM 3000-FINALIZAR.                                      00064001
            EXEC CICS                                                   00065001
              RETURN TRANSID ('T182')                                   00066001
            END-EXEC.                                                   00067001
      *-----------------------------------------------------            00068001
       0000-99-FIM.       EXIT.                                         00069001
      *-----------------------------------------------------            00070001
                                                                        00071001
      *----------------------------------------------------             00072001
       1000-INICIAR                              SECTION.               00073001
      *----------------------------------------------------             00074001
            EXEC CICS RECEIVE                                           00075001
               MAPSET('F18CIM1')                                        00076001
               MAP('MAPA01')                                            00077001
               INTO(MAPA01I)                                            00078001
               RESP(WRK-RET-MAPA)                                       00079001
            END-EXEC.                                                   00080001
                                                                        00090001
            IF WRK-RET-MAPA EQUAL DFHRESP(MAPFAIL)                      00091001
               MOVE 2 TO WRK-FLAG                                       00092001
            ELSE                                                        00093001
               MOVE 1 TO WRK-FLAG                                       00094001
            END-IF.                                                     00095001
      *-----------------------------------------------------            00096001
       1000-99-FIM.       EXIT.                                         00097001
      *-----------------------------------------------------            00098001
                                                                        00099001
      *----------------------------------------------------             00100001
       2000-PROCESSAR                            SECTION.               00110001
      *----------------------------------------------------             00120001
                                                                        00130001
             IF EIBAID = DFHPF10                                        00131001
                 EXEC CICS SEND                                         00132001
                   FROM(WRK-SAIDA)                                      00132101
                   ERASE                                                00132201
                 END-EXEC                                               00132301
                                                                        00132401
                 EXEC CICS                                              00132501
                   RETURN                                               00132601
                 END-EXEC                                               00132701
             END-IF.                                                    00132801
                                                                        00132901
             IF EIBAID = DFHENTER                                       00133001
                INITIALIZE MAPA01O REPLACING ALPHANUMERIC BY SPACES     00133101
                                             NUMERIC      BY ZEROES     00133201
                MOVE ZEROES TO IDO                                      00133301
             END-IF.                                                    00133401
                                                                        00133501
             IF EIBAID = DFHPF5                                         00133601
                MOVE IDI TO WRK-CHAVE                                   00133701
                   EXEC CICS READ                                       00133801
                             FILE ('FUNC')                              00133901
                             RIDFLD(WRK-CHAVE)                          00134001
                             INTO(WRK-DADOS)                            00134101
                             RESP(WRK-RESP)                             00134201
                   END-EXEC                                             00134301
                                                                        00134401
                     IF WRK-RESP EQUAL DFHRESP(NORMAL)                  00134501
                          MOVE WRK-NOME      TO NOMEO                   00134601
                          MOVE WRK-SETOR     TO SETORO                  00134701
                          MOVE WRK-SALARIO   TO SALARIOO                00134801
                          MOVE WRK-DATADIA   TO DATADIAO                00134901
                          MOVE WRK-DATAMES   TO DATAMESO                00135001
                          MOVE WRK-DATAANO   TO DATAANOO                00135101
                          MOVE WRK-EMAIL     TO EMAILO                  00135201
                          IF WRK-SALARIO EQUAL ZEROES OR                00135301
                             WRK-SALARIO IS NOT NUMERIC                 00135401
                             MOVE 'ENCONTRADO- SALARIO INCOERENTE'      00135501
                                   TO MSGO                              00135601
                          ELSE                                          00135701
                             MOVE 'ENCONTRADO'  TO MSGO                 00135801
                          END-IF                                        00135901
                     END-IF                                             00136001
                     IF WRK-RESP EQUAL DFHRESP(NOTFND)                  00136101
                          MOVE 'NAO EXISTE ' TO MSGO                    00136201
                     END-IF                                             00136301
             END-IF.                                                    00136401
                                                                        00136501
      *-----------------------------------------------------            00136601
       2000-99-FIM.       EXIT.                                         00136701
      *-----------------------------------------------------            00136801
      *-----------------------------------------------------            00136901
       3000-FINALIZAR                          SECTION.                 00137001
      *-----------------------------------------------------            00138001
            IF WRK-FLAG EQUAL 2                                         00139001
                EXEC CICS SEND                                          00140001
                   MAPSET('F18CIM1')                                    00141001
                   MAP('MAPA01')                                        00142001
                   ERASE                                                00143001
                   MAPONLY                                              00144001
                END-EXEC                                                00145001
            ELSE                                                        00146001
                EXEC CICS SEND                                          00147001
                   MAPSET('F18CIM1')                                    00148001
                   MAP('MAPA01')                                        00149001
                   FROM(MAPA01O)                                        00149101
                   DATAONLY                                             00149201
                END-EXEC                                                00149301
            END-IF.                                                     00149401
      *-----------------------------------------------------            00149501
       3000-99-FIM.       EXIT.                                         00149601
      *-----------------------------------------------------            00149701
