      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
                                                                        00031000
       PROGRAM-ID. MD021801.                                            00032000
                                                                        00033000
      *=========================================================*       00034000
      *  AUTOR      : PITER FANTI                               *       00034100
      *  INSTRUTOR  : IVAN SANCHES                              *       00034200
      *  EMPRESA    : FOURSYS                                   *       00034400
      *---------------------------------------------------------*       00034500
      *  VERSAO   DATA       AUTOR     DESCRICAO DA MODIFICACAO *       00034600
      *   1.0   06/07/22  PITER FANTI    VERSAO INICIAL         *       00034701
      *---------------------------------------------------------*       00034900
      *  OBJETIVO   : LER O ARQUIVO EVSA0407, FORMATAR DADOS EM *       00035001
      *               OUTRO ARQUIVO SEQUENCIAL COM REGISTROS    *       00035101
      *               ONDE SALARIO > 0; (MENORES DESPREZADOS)   *       00035215
      *               NA SYSOUT (COMECO DE PADRONIZACAO DE BOAS *       00035301
      *               PRATICAS PARA BANCO);                     *       00035415
      *               SVSA0407 = REGISTROS CONFORME             *       00035515
      *               S0407EXC = REGISTROS NAO CONFORME         *       00035615
      *---------------------------------------------------------*       00035900
      *  ARQUIVOS   :                                           *       00036000
      *   DDNAME          I/O                INCLUDE/BOOK       *       00036100
      *   EVSA0407         I                       -            *       00036302
      *   SVSA0407         O                    I#BV0407        *       00036403
      *   S0407EXC         O                    I#BV0407        *       00036515
      *---------------------------------------------------------*       00036615
      *  MODULOS    :                        INCLUDE/BOOK       *       00036715
      *   GRAVALOG         I                    #GLOGPT         *       00036815
      *=========================================================*       00036900
                                                                        00037000
      *====================================================             00037100
       ENVIRONMENT                               DIVISION.              00037200
      *====================================================             00037300
       INPUT-OUTPUT                              SECTION.               00037400
       FILE-CONTROL.                                                    00037500
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00037601
               ORGANIZATION IS INDEXED                                  00037701
               ACCESS MODE IS SEQUENTIAL                                00037805
               RECORD KEY IS EVSA-CHAVE                                 00037901
               FILE STATUS IS WRK-FS-EVSA0407.                          00038001
                                                                        00038101
           SELECT SVSA0407 ASSIGN TO SVSA0407                           00038202
               FILE STATUS IS WRK-FS-SVSA0407.                          00038302
                                                                        00039000
           SELECT S0407EXC ASSIGN TO S0407EXC                           00040014
               FILE STATUS IS WRK-FS-S0407EXC.                          00041014
                                                                        00042000
      *====================================================             00046000
       DATA                                      DIVISION.              00047000
      *====================================================             00048000
       FILE                                    SECTION.                 00049000
       FD EVSA0407.                                                     00050005
                                                                        00050103
      *-------LRECL19-----------                                        00051003
       01 FD-EVSA0407.                                                  00060001
          05 EVSA-CHAVE.                                                00070001
             10 EVSA-AGENCIA PIC X(04).                                 00080001
             10 EVSA-CONTA   PIC X(05).                                 00081001
          05 EVSA-SALARIO    PIC X(10).                                 00082001
                                                                        00090000
                                                                        00120000
                                                                        00144000
       FD SVSA0407                                                      00144106
            RECORDING MODE IS F                                         00144202
            BLOCK CONTAINS 0 RECORDS.                                   00144302
                                                                        00144402
      *---LRECL-27-(19+DATA(08))-------                                 00144504
       01 FD-SVSA0407-REG    PIC X(27).                                 00144607
                                                                        00144702
                                                                        00147000
      *----------------------------------------------                   00148000
       FD S0407EXC                                                      00149014
           RECORDING MODE IS F                                          00150014
           LABEL RECORD IS STANDARD                                     00151014
           BLOCK CONTAINS 0 RECORDS.                                    00152014
                                                                        00153000
      *--------LRECL-27-EXCESSOES---                                    00154014
       01 FD-S0407EXC-REG PIC X(27).                                    00155014
                                                                        00156000
                                                                        00156900
      *----------------------------------------------------             00157000
       WORKING-STORAGE                         SECTION.                 00158000
      *----------------------------------------------------             00159000
                                                                        00160000
      *------------------------------                                   00161001
       01 FILLER PIC X(30) VALUE 'VARIAVEL DE GRAVACAO'.                00161102
      *------------------------------                                   00161201
      *------LRECL-27----------                                         00161303
           COPY 'I#BV0407'.                                             00162003
                                                                        00168000
      *------------------------------                                   00168102
       01 FILLER PIC X(30) VALUE 'AREA DE LOG DE ERROS'.                00168202
      *------------------------------                                   00168302
      *------LRECL-------------                                         00168403
           COPY '#GLOGPT'.                                              00170000
                                                                        00180000
      *------------------------------                                   00180104
       01 FILLER PIC X(30) VALUE 'RECUPERAR DATA'.                      00180204
      *------------------------------                                   00180304
       01 WRK-DATA.                                                     00180404
          05 WRK-DATA-ANO PIC 9(04).                                    00180504
          05 WRK-DATA-MES PIC 9(02).                                    00180604
          05 WRK-DATA-DIA PIC 9(02).                                    00180704
                                                                        00180816
       01 WRK-DATA-ED.                                                  00180916
          05 WRK-DATA-DIA PIC 9(02).                                    00181016
          05 FILLER       PIC X(01) VALUE '/'.                          00181116
          05 WRK-DATA-MES PIC 9(02).                                    00181216
          05 FILLER       PIC X(01) VALUE '/'.                          00181316
          05 WRK-DATA-ANO PIC 9(04).                                    00181416
                                                                        00181604
      *------------------------------                                   00181704
       01 FILLER PIC X(30) VALUE 'AREA DE DISPLAY'.                     00181804
      *------------------------------                                   00181904
       01 WRK-FINAL-REL.                                                00182000
          05 FILLER PIC X(37) VALUE                                     00184010
                  '-------FINAL DO PROCESSAMENTO-------'.               00185010
                                                                        00185102
       77 WRK-LINBRANC    PIC X(47) VALUE SPACES.                       00185202
                                                                        00186000
      *---------------------------------                                00187002
       77 FILLER PIC X(20) VALUE 'AREA DE STATUS'.                      00187102
      *---------------------------------                                00187202
       77 WRK-FS-EVSA0407 PIC 9(02).                                    00188002
       77 WRK-FS-SVSA0407 PIC 9(02).                                    00189002
       77 WRK-FS-S0407EXC PIC 9(02).                                    00189114
                                                                        00189500
      *---------------------------------                                00189702
       77 FILLER PIC X(15) VALUE 'CONTADORES'.                          00189802
      *---------------------------------                                00189902
       77 WRK-REGLIDOS     PIC 9(03) VALUE ZEROES.                      00190000
       77 WRK-REGRAVADOS   PIC 9(03) VALUE ZEROES.                      00190103
       77 WRK-N-REGRAVADOS PIC 9(03) VALUE ZEROES.                      00190204
                                                                        00190300
      *-------LOG-DE-ERROS-------------                                 00191300
       77 WRK-GRAVALOG PIC X(08) VALUE 'GRAVALOG'.                      00191400
                                                                        00191500
      *====================================================             00192100
       PROCEDURE                                 DIVISION.              00192200
      *====================================================             00192300
                                                                        00192400
      *----------------------------------------------------             00192500
       0000-PRINCIPAL                            SECTION.               00192600
      *----------------------------------------------------             00192700
           PERFORM 1000-INICIAR.                                        00192800
             PERFORM 1200-TESTE-VAZIO.                                  00192900
           PERFORM 2000-PROCESSAR UNTIL WRK-FS-EVSA0407 EQUAL 10.       00193002
           PERFORM 3000-FINALIZAR.                                      00193200
                                                                        00193300
           STOP RUN.                                                    00193400
      *----------------------------------------------------             00193500
       0000-99-FIM.                       EXIT.                         00193600
      *----------------------------------------------------             00193700
                                                                        00193800
      *----------------------------------------------------             00193900
       1000-INICIAR                              SECTION.               00194000
      *----------------------------------------------------             00194100
           PERFORM 1100-ABRIR-ARQUIVOS.                                 00194200
           PERFORM 4000-TESTE-STATUS.                                   00194300
           PERFORM 1150-RECEBER-DATA.                                   00194418
      *----------------------------------------------------             00194600
       1000-99-FIM.             EXIT.                                   00194700
      *----------------------------------------------------             00194800
                                                                        00194900
      *----------------------------------------------------             00195000
       1100-ABRIR-ARQUIVOS                       SECTION.               00195100
           OPEN INPUT  EVSA0407                                         00195202
                OUTPUT SVSA0407 S0407EXC.                               00196014
       1100-99-FIM.                   EXIT.                             00197000
      *----------------------------------------------------             00198000
                                                                        00199000
      *----------------------------------------------------             00199116
       1150-RECEBER-DATA                         SECTION.               00199218
            ACCEPT WRK-DATA FROM DATE YYYYMMDD.                         00199316
            MOVE CORR WRK-DATA TO WRK-DATA-ED.                          00199416
       1150-99-FIM.                   EXIT.                             00199616
      *----------------------------------------------------             00199716
                                                                        00199816
      *----------------------------------------------------             00200000
       1200-TESTE-VAZIO                           SECTION.              00201000
           READ EVSA0407.                                               00203016
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROES                          00204016
              MOVE 'REGISTRO NAO ENCONTRADO' TO WRK-MSGERRO             00204116
              DISPLAY WRK-MSGERRO                                       00204216
              DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                   00204317
              GOBACK                                                    00204416
           END-IF.                                                      00204516
       1200-99-FIM.                   EXIT.                             00206000
      *----------------------------------------------------             00207000
                                                                        00207100
      *----------------------------------------------------             00208100
       2000-PROCESSAR                            SECTION.               00208200
      *----------------------------------------------------             00208300
            ADD 1 TO WRK-REGLIDOS.                                      00208404
                                                                        00208504
            PERFORM 2100-MOVE-DADOS                                     00208604
            IF EVSA-SALARIO EQUAL ZEROES                                00208713
               OR EVSA-SALARIO IS NOT NUMERIC                           00209013
               WRITE FD-S0407EXC-REG FROM WRK-CORPO                     00209114
               ADD 1 TO WRK-N-REGRAVADOS                                00209213
            ELSE                                                        00210004
               WRITE FD-SVSA0407-REG FROM WRK-CORPO                     00211013
               ADD 1 TO WRK-REGRAVADOS                                  00212013
            END-IF.                                                     00213004
                                                                        00214004
            READ EVSA0407.                                              00215004
                                                                        00216000
      *----------------------------------------------------             00217000
       2000-99-FIM.             EXIT.                                   00218000
      *----------------------------------------------------             00219000
                                                                        00310600
      *----------------------------------------------------             00310700
       2100-MOVE-DADOS                           SECTION.               00310802
                                                                        00310900
           MOVE FD-EVSA0407    TO WRK-CORPO.                            00311102
           MOVE WRK-DATA       TO WRK-SVSA0407-DADOS-DATA-GRAV.         00311210
                                                                        00311700
       2100-99-FIM.        EXIT.                                        00311800
      *----------------------------------------------------             00311900
                                                                        00312000
                                                                        00313000
      *----------------------------------------------------             00314000
       3000-FINALIZAR                            SECTION.               00314100
      *----------------------------------------------------             00314200
           PERFORM 3100-DISPLAY-FINAL.                                  00314400
           PERFORM 3200-FECHAR.                                         00314500
      *----------------------------------------------------             00314600
       3000-99-FIM.             EXIT.                                   00314700
      *----------------------------------------------------             00314800
                                                                        00314900
      *----------------------------------------------------             00316200
       3100-DISPLAY-FINAL                        SECTION.               00316300
           DISPLAY WRK-LINBRANC.                                        00316600
           DISPLAY 'REG LIDOS        : ' WRK-REGLIDOS.                  00316719
           DISPLAY 'REG GRAVADOS     : ' WRK-REGRAVADOS.                00316819
           DISPLAY 'REG NAO GRAVADOS : ' WRK-N-REGRAVADOS.              00317019
           DISPLAY WRK-LINBRANC.                                        00317216
           DISPLAY 'DATA DO PROCESSAMENTO : ' WRK-DATA-ED.              00317319
           DISPLAY WRK-LINBRANC.                                        00317416
           DISPLAY WRK-FINAL-REL.                                       00318004
       3100-99-FIM.         EXIT.                                       00319000
      *----------------------------------------------------             00320000
                                                                        00330000
      *----------------------------------------------------             00340000
       3200-FECHAR                               SECTION.               00341000
           CLOSE EVSA0407 SVSA0407 S0407EXC.                            00342014
       3200-99-FIM.         EXIT.                                       00343000
      *----------------------------------------------------             00344000
                                                                        00345000
                                                                        00346000
                                                                        00347000
      *----------------------------------------------------             00347100
       4000-TESTE-STATUS                         SECTION.               00347200
      *----------------------------------------------------             00347300
           PERFORM 4100-STATUS-EVSA0407.                                00347402
           PERFORM 4200-STATUS-SVSA0407.                                00347502
           PERFORM 4300-STATUS-S0407EXC.                                00347614
      *----------------------------------------------------             00347800
       4000-99-FIM.               EXIT.                                 00347900
      *----------------------------------------------------             00348000
                                                                        00349000
      *----------------------------------------------------             00350000
       4100-STATUS-EVSA0407                      SECTION.               00360002
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROES                          00361002
              MOVE '1100'                  TO WRK-SECAO                 00362000
              MOVE 'ERRO NO OPEN EVSA0407' TO WRK-MSGERRO               00362102
              MOVE WRK-FS-EVSA0407         TO WRK-STATUS                00362202
              PERFORM 9000-TRATA-ERROS                                  00362300
           END-IF.                                                      00362400
       4100-99-FIM.    EXIT.                                            00362500
      *----------------------------------------------------             00362600
                                                                        00362700
      *----------------------------------------------------             00362800
       4200-STATUS-SVSA0407                      SECTION.               00362902
           IF WRK-FS-SVSA0407 NOT EQUAL ZEROES                          00363002
              MOVE '1100'                  TO WRK-SECAO                 00364002
              MOVE 'ERRO NO OPEN SVSA0407' TO WRK-MSGERRO               00365002
              MOVE WRK-FS-SVSA0407         TO WRK-STATUS                00365102
              PERFORM 9000-TRATA-ERROS                                  00365202
           END-IF.                                                      00365302
       4200-99-FIM.    EXIT.                                            00365402
      *----------------------------------------------------             00365500
                                                                        00365600
      *----------------------------------------------------             00365700
       4300-STATUS-S0407EXC                      SECTION.               00365814
           IF WRK-FS-S0407EXC NOT EQUAL ZEROES                          00365914
              MOVE '1100'                  TO WRK-SECAO                 00366014
              MOVE 'ERRO NO OPEN S0407EXC' TO WRK-MSGERRO               00366114
              MOVE WRK-FS-S0407EXC         TO WRK-STATUS                00366214
              PERFORM 9000-TRATA-ERROS                                  00366314
           END-IF.                                                      00366414
       4300-99-FIM.    EXIT.                                            00366514
      *----------------------------------------------------             00366600
                                                                        00366700
      *----------------------------------------------------             00367900
       9000-TRATA-ERROS                          SECTION.               00368000
      *----------------------------------------------------             00369000
           MOVE 'MD021801' TO WRK-PROGRAMA.                             00369102
           DISPLAY WRK-MSGERRO.                                         00369202
           DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407.                     00369302
           DISPLAY WRK-FINAL-REL.                                       00369402
      *    CALL WRK-GRAVALOG USING WRK-DADOS.                           00369502
           GOBACK.                                                      00369600
      *----------------------------------------------------             00369700
       9000-99-FIM.             EXIT.                                   00369800
      *----------------------------------------------------             00369900
