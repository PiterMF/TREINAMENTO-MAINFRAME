      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
                                                                        00031000
       PROGRAM-ID. REL1804.                                             00032000
                                                                        00033000
      *====================================================             00034000
      *  AUTOR   : PITER FANTI                                          00035000
      *  EMPRESA : FOURSYS                                              00036000
      *----------------------------------------------------             00037000
      *  DATA    : 24/05/2022                                           00038000
      *  OBJETIVO: LER TODOS OS FUNCIONARIOS E IMPRIMIR UM              00039000
      *            RELATORIO POR SETOR E SOMANDO SALARIOS               00040000
      *====================================================             00050000
                                                                        00060000
      *====================================================             00070000
       ENVIRONMENT                               DIVISION.              00080000
      *====================================================             00090000
       INPUT-OUTPUT                              SECTION.               00100000
       FILE-CONTROL.                                                    00110000
           SELECT FUNC ASSIGN TO FUNC                                   00120000
               FILE STATUS IS WRK-FS-FUNC.                              00130000
                                                                        00140000
      *====================================================             00141000
       DATA                                      DIVISION.              00142000
      *====================================================             00143000
       FILE                                    SECTION.                 00144000
       FD FUNC                                                          00145000
           RECORDING MODE IS F                                          00146000
           LABEL RECORD IS STANDARD                                     00147000
           BLOCK CONTAINS 0 RECORDS.                                    00148000
                                                                        00149000
      *-------LRECL-29----------                                        00150000
       01 FD-FUNC.                                                      00160010
          05 FD-IDFUNC    PIC 9(05).                                    00161010
          05 FD-NOMEFUNC  PIC X(30).                                    00162010
          05 FD-SETORFUNC PIC X(04).                                    00163015
          05 FD-SALFUNC   PIC 9(06).                                    00164010
                                                                        00170000
      *----------------------------------------------------             00180000
       WORKING-STORAGE                         SECTION.                 00181000
      *----------------------------------------------------             00182000
      *---STATUS-DA-OPERACAO-------------------                         00183000
       01 WRK-FUNC.                                                     00184010
          05 WRK-IDFUNC    PIC 9(05).                                   00185000
          05 FILLER        PIC X(04).                                   00185101
          05 WRK-NOMEFUNC  PIC X(30).                                   00186000
          05 WRK-SETORFUNC PIC X(04).                                   00187015
          05 FILLER        PIC X(08).                                   00187101
          05 WRK-SALFUNC   PIC 9(06).                                   00187200
                                                                        00187305
      *--------------TAMANHO DA LINHA 58                                00187407
       01 WRK-CABEC.                                                    00187801
          05 WRK-CABEC1.                                                00187901
             10 FILLER  PIC X(12).                                      00188003
             10 FILLER  PIC X(37) VALUE                                 00188103
             'RELATORIO DE FUNCIONARIOS      PAG: '.                    00188201
             10 WRK-PAG PIC 9(02) VALUE 1.                              00188301
             10 FILLER  PIC X(07).                                      00188403
          05 WRK-CABEC2.                                                00188601
             10 FILLER  PIC X(09) VALUE 'ID       '.                    00188703
             10 FILLER  PIC X(04) VALUE 'NOME'.                         00188803
             10 FILLER  PIC X(26).                                      00188903
             10 FILLER  PIC X(12) VALUE 'SETOR       '.                 00189003
             10 FILLER  PIC X(07) VALUE 'SALARIO'.                      00189103
             10 FILLER  PIC X(01).                                      00189207
                                                                        00189300
           COPY '#GLOGPT'.                                              00189400
                                                                        00189500
       77 WRK-FS-FUNC  PIC 9(02).                                       00189600
       77 WRK-CONTFUNC PIC 9(02)      VALUE ZEROES.                     00189700
       77 WRK-LINHA    PIC 9(02)      VALUE 1.                          00189807
       77 WRK-SOMAPAG  PIC 9(08) COMP VALUE ZEROES.                     00189907
       77 WRK-SOMATOT  PIC 9(08) COMP VALUE ZEROES.                     00190007
      *====================================================             00190100
       PROCEDURE                                 DIVISION.              00190200
      *====================================================             00190300
                                                                        00190400
      *----------------------------------------------------             00190500
       0000-PRINCIPAL                            SECTION.               00190600
      *----------------------------------------------------             00191000
           PERFORM 1000-INICIAR.                                        00192000
           PERFORM 2000-TESTE-VAZIO.                                    00193000
           PERFORM 3000-PROCESSAR UNTIL WRK-FS-FUNC EQUAL 10.           00194000
           PERFORM 4000-FINALIZAR.                                      00195000
                                                                        00196000
           STOP RUN.                                                    00197000
      *----------------------------------------------------             00198000
       0000-99-FIM.                       EXIT.                         00199000
      *----------------------------------------------------             00200000
                                                                        00201000
      *----------------------------------------------------             00202000
       1000-INICIAR                              SECTION.               00203000
      *----------------------------------------------------             00204000
           PERFORM 1100-ABRIR-ARQUIVOS.                                 00205014
           PERFORM 5000-TESTE-STATUS.                                   00205100
           READ FUNC.                                                   00205214
           PERFORM 1050-DISP-CABEC.                                     00205414
      *----------------------------------------------------             00205600
       1000-99-FIM.             EXIT.                                   00205700
      *----------------------------------------------------             00205800
                                                                        00205907
      *----------------------------------------------------             00206007
       1050-DISP-CABEC                           SECTION.               00206107
           DISPLAY ' '.                                                 00206207
           DISPLAY WRK-CABEC1.                                          00206307
           DISPLAY ' '.                                                 00206407
           DISPLAY WRK-CABEC2.                                          00206507
           ADD 4 TO WRK-LINHA.                                          00206607
       1050-99-FIM.            EXIT.                                    00206707
      *----------------------------------------------------             00206807
                                                                        00206900
      *----------------------------------------------------             00207000
       1100-ABRIR-ARQUIVOS                       SECTION.               00207100
           OPEN INPUT FUNC.                                             00207200
       1100-99-FIM.                   EXIT.                             00207300
      *----------------------------------------------------             00207400
                                                                        00207500
      *----------------------------------------------------             00208000
       2000-TESTE-VAZIO.                                                00209000
             IF WRK-FS-FUNC EQUAL 10                                    00220000
               MOVE 'FIM DO ARQUIVO' TO WRK-MSGERRO                     00230000
               PERFORM 9000-TRATA-ERROS                                 00240007
             END-IF.                                                    00290000
       2000-99-FIM.                   EXIT.                             00300000
      *----------------------------------------------------             00301000
                                                                        00302000
      *----------------------------------------------------             00303000
       3000-PROCESSAR                            SECTION.               00304000
      *----------------------------------------------------             00305000
           ADD 1 TO WRK-CONTFUNC.                                       00305107
           MOVE FD-IDFUNC    TO WRK-IDFUNC.                             00305210
           MOVE FD-NOMEFUNC  TO WRK-NOMEFUNC.                           00305310
           MOVE FD-SETORFUNC TO WRK-SETORFUNC.                          00305410
           MOVE FD-SALFUNC   TO WRK-SALFUNC.                            00305510
              ADD WRK-SALFUNC TO WRK-SOMAPAG.                           00306114
              ADD WRK-SALFUNC TO WRK-SOMATOT.                           00306214
               DISPLAY WRK-FUNC.                                        00306314
                ADD 1 TO WRK-LINHA.                                     00306414
                 IF WRK-LINHA  GREATER 8                                00306513
                    DISPLAY 'SOMA DE SALARIOS..: ' WRK-SOMAPAG          00306610
                    MOVE ZEROES TO WRK-SOMAPAG                          00306710
                    MOVE 1 TO WRK-LINHA                                 00306810
                    PERFORM 1050-DISP-CABEC                             00306910
                 END-IF.                                                00307114
           READ FUNC.                                                   00307314
           IF WRK-FS-FUNC EQUAL 10                                      00307414
              DISPLAY 'SOMA DE SALARIOS..: ' WRK-SOMAPAG                00307514
              DISPLAY 'FIM DO ARQUIVO'                                  00307614
           END-IF.                                                      00307814
      *----------------------------------------------------             00307910
       3000-99-FIM.             EXIT.                                   00308000
      *----------------------------------------------------             00309000
                                                                        00310000
                                                                        00320000
      *----------------------------------------------------             00330000
       4000-FINALIZAR                            SECTION.               00340000
      *----------------------------------------------------             00341000
           PERFORM 4100-DISPLAY-FINAL.                                  00342000
           PERFORM 4200-FECHAR.                                         00343000
                                                                        00344000
      *----------------------------------------------------             00345000
       4100-DISPLAY-FINAL                        SECTION.               00346000
           DISPLAY ' '.                                                 00346107
           DISPLAY 'SOMA TOTAL DE SALARIOS: ' WRK-SOMATOT.              00346207
           DISPLAY 'REGISTROS LIDOS....: ' WRK-CONTFUNC.                00347004
           DISPLAY ' '.                                                 00347107
           DISPLAY '------------FINAL DO RELATORIO-----------'.         00347207
       4100-99-FIM.         EXIT.                                       00348000
      *----------------------------------------------------             00349000
                                                                        00350000
      *----------------------------------------------------             00360000
       4200-FECHAR                               SECTION.               00361000
           CLOSE FUNC.                                                  00361200
       4200-99-FIM.         EXIT.                                       00361300
      *----------------------------------------------------             00361400
                                                                        00361500
      *----------------------------------------------------             00361600
       4000-99-FIM.             EXIT.                                   00361700
      *----------------------------------------------------             00361800
                                                                        00361900
                                                                        00362000
      *----------------------------------------------------             00362100
       5000-TESTE-STATUS                         SECTION.               00363000
      *----------------------------------------------------             00364000
           IF WRK-FS-FUNC NOT EQUAL ZEROES                              00365000
              MOVE '1100'              TO WRK-SECAO                     00366000
              MOVE 'ERRO NO OPEN FUNC' TO WRK-MSGERRO                   00367000
              MOVE WRK-FS-FUNC         TO WRK-STATUS                    00368000
              PERFORM 9000-TRATA-ERROS                                  00369000
           END-IF.                                                      00370000
      *----------------------------------------------------             00380000
       5000-99-FIM.               EXIT.                                 00390000
      *----------------------------------------------------             00400000
                                                                        00410000
      *----------------------------------------------------             00420000
       9000-TRATA-ERROS                          SECTION.               00430000
      *----------------------------------------------------             00440000
           MOVE 'REL1803' TO WRK-PROGRAMA.                              00450000
           CALL 'GRAVALOG' USING WRK-DADOS.                             00460000
           GOBACK.                                                      00470000
      *----------------------------------------------------             00480000
       9000-99-FIM.             EXIT.                                   00490000
      *----------------------------------------------------             00500000
